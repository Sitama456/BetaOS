## 硬盘读写
boot.bin的职责是读取loader.bin模块，涉及到硬盘读取操作。此时没有操作系统，只能通过最底层的手段访问硬盘。

- 扇区：硬盘读写的最小单位，最多 256 个扇区
- 机械臂的寻道时间是硬盘性能的主要瓶颈
- 一般情况下一个磁道有 63 个扇区主要是由于BIOS最大支持这么多
- 磁道从外侧计数，所以一般情况下 C 盘的读写速度最快；

### 硬盘读写方式
- CHS 模式 / Cylinder / Head / Sector
- LBA 模式 / Logical Block Address
现在主要使用LBA模式，LBA28，总共能访问128G的磁盘空间。

| Primary 通道           | Secondary 通道 | in 操作      | out 操作     |
|------------------------|----------------|--------------|--------------|
| 0x1F0                   | 0x170          | Data         | Data         |
| 0x1F1                   | 0x171          | Error        | Features     |
| 0x1F2                   | 0x172          | Sector count | Sector count |
| 0x1F3                   | 0x173          | LBA low      | LBA low      |
| 0x1F4                   | 0x174          | LBA mid      | LBA mid      |
| 0x1F5                   | 0x175          | LBA high     | LBA high     |
| 0x1F6                   | 0x176          | Device       | Device       |
| 0x1F7                   | 0x177          | Status       | Command      |

- 0x1F0：16bit 端口，用于读写数据
- 0x1F1：检测前一个指令的错误
- 0x1F2：读写扇区的数量
- 0x1F3：起始扇区的 0 ~ 7 位
- 0x1F4：起始扇区的 8 ~ 15 位
- 0x1F5：起始扇区的 16 ~ 23 位
- 0x1F6:
    - 0 ~ 3：起始扇区的 24 ~ 27 位
    - 4: 0 主盘, 1 从片
    - 6: 0 CHS, 1 LBA
    - 5 ~ 7：固定为1
- 0x1F7: out
    - 0xEC: 识别硬盘
    - 0x20: 读硬盘
    - 0x30: 写硬盘
- 0x1F7: in / 8bit
    - 0 ERR
    - 3 DRQ 数据准备完毕
    - 7 BSY 硬盘繁忙

### 读硬盘汇编程序
```asm
; 读取loader到内存中
mov edi, 0x1000         ; 读取的目标内存
mov ecx, 2              ; 起始扇区
mov bl, 4               ; 扇区数量
call read_disk

read_disk:
    ; 设置读写扇区的数量
    mov dx, 0x1f2
    mov al, bl
    out dx, al

    inc dx              ; 0x1f3
    mov al, cl          ; 起始扇区的前8位
    out dx, al

    inc dx              ; 0x1f4
    shr ecx, 8
    mov al, cl          ; 起始扇区中8位
    out dx, al

    inc dx              ; 0x1f5
    shr ecx, 8
    mov al, cl          ; 起始扇区高8位
    out dx, al

    inc dx              ; 0x1f6
    shr ecx, 8
    and cl, 0b1111      ; 将高四位置为0

    mov al, 0b1110_0000 
    or al, cl
    out dx, al          ; 主盘 - LBA模式

    inc dx              ; 0x1f7
    mov al, 0x20        ; 读硬盘
    out dx, al

    xor ecx, ecx        ; 将 ecx清空
    mov cl, bl          ; 读取的扇区数量

    .read:
        push cx         ; 先保存cx，因为read会改变cx
        call .waits     ; 等待数据准备完毕
        call .reads      ; 读取一个扇区
        pop cx          ; 恢复 cx
        loop .read  
    ret

    .waits:
        mov dx, 0x1f7
        .check:
            in al, dx
            jmp $+2     ; nop 直接跳转到下一行
            jmp $+2     ; 一点点延迟
            jmp $+2     
            and al, 0b1000_1000
            cmp al, 0b0000_1000
            jnz .check  ; 还没有准备好
        ret
    
    .reads:
        mov dx, 0x1f0
        mov cx, 256     ; 一个扇区 256 字
        .readw:
            in ax, dx
            jmp $+2     ; 一点点延迟
            jmp $+2
            jmp $+2
            mov [edi], ax
            add edi, 2
            loop .readw
        ret
```

### 参考文献

- <https://www.youtube.com/watch?v=oEORcCQ62nQ>
- <https://wiki.osdev.org/ATA_PIO_Mode>
- <https://bochs.sourceforge.io/techspec/PORTS.LST>
- <https://www.techtarget.com/searchstorage/definition/IDE>